plugins {
    id 'java'
    id 'application'
}

group = 'javah4x'
version = '0.8.0'

repositories {
    jcenter()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testImplementation 'junit:junit:4.13'
    testCompile 'org.mockito:mockito-core:2.+'
}

compileTestJava {
    options.compilerArgs << '-parameters'
}

task executable {
    dependsOn 'jar'
    doLast {
        File scriptFile = new File(project.rootDir, "javah4x.sh")
        File jarFile = new File(project.buildDir, "libs/javah4x-${project.version}.jar")
        File outDir = new File(project.buildDir, "distributions")
        outDir.mkdirs()
        File outFile = new File(outDir, "javah4x-${project.version}")
        outFile.setBytes(new byte[0])
        outFile.append(scriptFile.newInputStream())
        outFile.append(jarFile.newInputStream())
        outFile.setExecutable(true)
    }
}
assemble.dependsOn('executable')

task executableZip(type: Zip) {
    dependsOn 'executable'
    from "${project.buildDir}/distributions/javah4x-${project.version}"
    destinationDirectory = new File(project.buildDir, "distributions")
    archiveFileName = "javah4x-bin-${project.version}.zip"
}
